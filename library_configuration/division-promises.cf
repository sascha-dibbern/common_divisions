#
# *cfdivisions_bundlesequence = main_library_configuration
# *cfdivisions_depends = 
#

body file control
{
    namespace => "common_divisions";
}

bundle agent main_library_configuration
{
  methods:
    "configuration" usebundle => library_configuration("common_divisions.json");
}

bundle agent library_configuration(file) 
{
  classes:
    any::
      "override_file_exists"
        expression => fileexists("$(sys.workdir)/inputs/override/$(file)");

      "have_library_configuration_data" 
        expression => regcmp(".*\{.*\}.*","$(configuration_string)"),
        scope      => "namespace";

    have_library_configuration_data::
      "$(classkeys)" 
        expression => "any", 
        scope      => "namespace";

  vars:
    override_file_exists::
      "configuration_file"
        string => "$(sys.workdir)/inputs/override/$(file)";

    !override_file_exists::
      "configuration_file"
        string => "$(sys.workdir)/inputs/$(file)";
    
    any::
      "configuration_data"
        data   => readjson("$(configuration_file)", "999999"),
        policy => "free";

      "configuration_string"
        string => format("%S",configuration_data),
        policy => "free";

      "classkeys"
        slist => getindices("configuration_data[classes]"),
        policy => "free";

      "varkeys"
        slist => getindices("configuration_data[vars]"),
        policy => "free";

      "$(varkeys)"
        string => "$(configuration_data[vars][$(varkeys)])",
        policy => "free";

  reports:
    DEBUG|DEBUG_library_configuration::
      "DEBUG $(this.bundle): Library 'common_divisions'";

      "DEBUG $(this.bundle): Got library_configuration data from $(file): $(configuration_string)"
        ifvarclass => "have_library_configuration_data";

      "DEBUG $(this.bundle): Got varkey $(varkeys)"
        ifvarclass => "have_library_configuration_data";

      "DEBUG $(this.bundle): Got variable * $(varkeys) = $(configuration_data[vars][$(varkeys)])"
        ifvarclass => "have_library_configuration_data";

      "DEBUG $(this.bundle): Got library_configuration class = $(classkeys)"
        ifvarclass => "have_library_configuration_data";

    !have_library_configuration_data::
      "ERROR $(this.bundle): Could not read the library_configuration data from $(configuration_file)";
}
