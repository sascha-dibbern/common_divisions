bundle common cfengine_stdlib
{
  vars:
    cfengine_3_5::
      # 3.5 doesn't have "body file control" so all the includes are listed here
      "inputs" slist => {
                          "lib/3.5/paths.cf",
                          "lib/3.5/common.cf",
                          "lib/3.5/commands.cf",
                          "lib/3.5/packages.cf",
                          "lib/3.5/files.cf",
                          "lib/3.5/services.cf",
                          "lib/3.5/processes.cf",
                          "lib/3.5/storage.cf",
                          "lib/3.5/databases.cf",
                          "lib/3.5/monitor.cf",
                          "lib/3.5/guest_environments.cf",
                          "lib/3.5/bundles.cf",
                          "lib/3.5/cfe_internal.cf",
      };

    !cfengine_3_5::
      # CFEngine 3.6 and higher can include through a secondary file
      "inputs" slist => { "../../$(sys.local_libdir)/stdlib.cf" };

  reports:
    any::
     "common cfengine_stdlib $(sys.local_libdir)";
    #verbose_mode::
      "$(this.bundle): defining inputs='$(inputs)'";
}

bundle common divisions
{
  classes:
    !common_divisions::
      # !!! ignore bundles hostroles_defined_by_library_configuration and hostroles_run in bundlessequence
      "common_divisions" expression => usemodule ("cfdivisions","--library=common_divisions --namespace=common_divisions --inputs_path=$(sys.workdir)/inputs --divisionfilter=hostroles --ignore_bundles=hostroles_defined_by_library_configuration,hostroles_run");
}


body common control
{
      inputs => {
                 @(cfengine_stdlib.inputs),
                 @(cfdivisions.cfdivisions_common_divisions_inputs)
      };

      bundlesequence => {
                          @(cfdivisions.cfdivisions_common_divisions_bundlesequence),
                          test_hostroles,
      };
}

bundle agent test_hostroles
{
  methods:
    "update" 
        usebundle => common_divisions:hostroles_configuration("common_divisions/hostroles/test-hostroles.txt","testhost");
    "run" 
        usebundle => common_divisions:hostroles_run;

  reports:
    any::
     "Bundlesequence: $(cfdivisions.cfdivisions_common_divisions_bundlesequence)";

    common_divisions:hostroles_configuration_location_defined::
      "    ok - hostroles_configuration_location_defined";
    !common_divisions:hostroles_configuration_location_defined::
      "    not ok - hostroles_configuration_location_defined";

    common_divisions:hostroles_module_exists::
      "    ok - hostroles_module_exists";
    !common_divisions:hostroles_module_exists::
      "    not ok - hostroles_module_exists";

    common_divisions:hostroles_module_executed::
      "    ok - hostroles_module_executed";
    !common_divisions:hostroles_module_executed::
      "    not ok - hostroles_module_executed";

    common_divisions:test1::
      "    ok - class test1 defined";
    !common_divisions:test1::
      "    not ok - class test1 defined";
    !common_divisions:test2::
      "    ok - class test2 undefined";
    common_divisions:test2::
      "    not ok - class test2 undefined";
}

